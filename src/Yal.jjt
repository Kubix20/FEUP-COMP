options {
  LOOKAHEAD=1;
  MULTI=true;
  IGNORE_CASE=true;
  NODE_DEFAULT_VOID=true;
  NODE_SCOPE_HOOK=true;
  TRACK_TOKENS=true;
  JJTREE_OUTPUT_DIRECTORY="tree";
  OUTPUT_DIRECTORY="tree";
}

PARSER_BEGIN(Yal)
package tree;
import java.io.File;
import java.io.FileReader;
import java.io.FileNotFoundException;
import semantic.*;
import generator.CodeGenerator;

public class Yal {
  public static int nErrors = 0;

  public static void printUsage() {
    System.out.println("Usage: java -jar yal2jvm.jar <file path:string> [OPTIONAL debug:1 for true, anything else false]");
  }
  public static void main(String args[]) throws ParseException, FileNotFoundException {

	if(args.length < 1){
		System.out.println("Wrong amount of arguments");
    Yal.printUsage();
		return;
	}

	File inputFile = new File(args[0]);
	if(!inputFile.exists() || !inputFile.isFile()){
		System.out.println("Unable to find source file: "+inputFile);
    Yal.printUsage();
		return;
	}

  boolean debug;
  if(args.length > 1 && Integer.parseInt(args[1]) == 1)
    debug = true;
  else
    debug = false;

	String newline = System.lineSeparator();
	System.out.println(newline+newline+
					   "<<<<<<<< Parsing file "+ args[0]+ " >>>>>>>>>"+
					   newline+newline);
	int errors;
    Yal parser = new Yal(new FileReader(args[0]));
    ASTModule root = parser.Module();
    root.dump(">");
	if(Yal.nErrors > 0){
		System.out.println("Program parsed with "+Yal.nErrors+" errors");
		return;
	}

	SymbolTable st = new SymbolTable(debug);
	st.analyse(root); //Semantic analysis
	st.print();
	errors = st.printErrors();
    if(errors > 0){
		System.out.println("Program parsed with "+errors+" errors");
		return;
	}
	st.printWarnings();

	CodeGenerator cg = new CodeGenerator(inputFile,root,st,debug);
	try{
		cg.generate();
	}
	catch(Exception e){
		e.printStackTrace();
		return;
	}

	System.out.println("Program compiled successfully!");
  }
}
PARSER_END(Yal)

<DEFAULT> SKIP : {
  " "
  | "\t"
  | "\n"
  | "\r"
  | <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
  | <"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/">
}
/* reserved words */
<DEFAULT> TOKEN : {
  <RELA_OP: ">" | "<" | "<=" | ">=" | "==" | "!=">
  | <ADDSUB_OP: "+" | "-">
  | <ARITH_OP: "*" | "/" | "<<" | ">>" | ">>>">
  | <BITWISE_OP: "&" | "|" | "^">
  | <NOT_OP: "!">
  | <WHILE: "while">
  | <IF: "if">
  | <ELSE: "else">
  | <ASSIGN: "=">
  | <ASPA: "\"">
  | <LPAR: "(">
  | <RPAR: ")">
  | <VIRG: ",">
  | <PVIRG: ";">
  | <LCHAVETA: "{">
  | <RCHAVETA: "}">
  | <FUNCTION: "function">
  | <MODULE: "module">
  | <SIZE: "size">
}
<DEFAULT> TOKEN : {
  <INTEGER: (<DIGIT>)+>
  | <ID: <LETTER> (<LETTER> | <DIGIT>)*>
  | <#LETTER: ["$","A"-"Z","_","a"-"z"]>
  | <#DIGIT: ["0"-"9"]>
  | <STRING: "\"" (["a"-"z","A"-"Z","0"-"9",":"," ","="])+ "\"">
}
JAVACODE
void jjtreeOpenNodeScope(Node n){
	((SimpleNode) n).setLine(getToken(1).beginLine);
}

JAVACODE
void jjtreeCloseNodeScope(Node n){}


JAVACODE
void error_skipto(ParseException e, int kind, String whereWasCaught) {
  ++Yal.nErrors;
  System.out.println("EXCEPTION CAUGHT IN "+whereWasCaught+": "+e.toString());  // print the error message
  Token t;
  do {
    t = getNextToken();
  } while (t.kind != kind);
  System.out.println("Proceeding with "+t.image+" of kind "+t.kind);
}


JAVACODE
void error_skipto_lookahead(ParseException e, int kind, String whereWasCaught) {
  ++Yal.nErrors;
  System.out.println("EXCEPTION CAUGHT IN "+whereWasCaught+": "+e.toString());  // print the error message
  Token t = e.currentToken;
  while(getToken(1).kind != kind){
    t = getNextToken();
  }
  System.out.println("Proceeding with "+t.image+" of kind "+t.kind);
}

JAVACODE
boolean contains(int[] kinds, int kind) {
  for(int i=0; i<kinds.length; i++) {
	  if(kinds[i] == kind)
		  return true;
  }
  return false;
}

JAVACODE
void error_multskipto(ParseException e, int[] kinds, String whereWasCaught) {
  ++Yal.nErrors;
  System.out.println("EXCEPTION CAUGHT IN "+whereWasCaught+": "+e.toString());  // print the error message
  Token t;
  do {
    t = getNextToken();
  } while (!contains(kinds,t.kind));
  System.out.println("Proceeding with "+t.image+" of kind "+t.kind);
}

JAVACODE
void error(ParseException e, String whereWasCaught) {
  ++Yal.nErrors;
  System.out.println("EXCEPTION CAUGHT IN "+whereWasCaught+": "+e.toString());  // print the error message
}

ASTModule Module() #Module : {Token t1;}
{
  <MODULE> t1=<ID>{jjtThis.setValues(t1.image);} <LCHAVETA> (Declaration())* (Function())*
  try {
	<RCHAVETA>
  }
  catch(ParseException e){
    error_skipto(e, RCHAVETA, "BEFORE MODULE END");
  }
  {return jjtThis;}
}

void Declaration() #Declaration : {Token t1;}
{
  try{
	Element() [t1=<ASSIGN>{jjtThis.setValues(t1.image);} (("[" ArraySize() "]") | IntElement())] <PVIRG>
  }
  catch(ParseException e){
    error_skipto(e, PVIRG, "DECLARATION");
  }
}

void Function() #Function : {Token t1;}
{
  try {
    <FUNCTION> [LOOKAHEAD(2) Element() <ASSIGN>] t1=<ID>{jjtThis.setValues(t1.image);} <LPAR>
  }
  catch(ParseException e){
    error_skipto(e, LPAR, "FUNCTION DECLARATION");
  }

  try {
    [Varlist()] <RPAR>
  }
  catch(ParseException e){
    error_skipto(e, RPAR, "FUNCTION DECLARATION ARGS");
  }

    <LCHAVETA> Stmtlst() <RCHAVETA>
}

void IntElement() #IntElement : {Token t1 = null, t2 = null;}
{
  [t1=<ADDSUB_OP>] t2=<INTEGER>{jjtThis.setValues((t1 == null ? "": t1.image), (t2 == null ? "": t2.image));}
}

void Varlist() #Varlist : {}
{
  (Element()) (<VIRG> (Element()))*
}

void Element() #Element : {Token t1 = null, t2 = null, t3 = null;}
{
  t1=<ID> [t2="[""]"]{jjtThis.setValues((t1 == null ? "": t1.image), (t2 == null ? "": t2.image));}
}

void CmpStmtLst() : {}
{
   <LCHAVETA> Stmtlst() <RCHAVETA>
}

void Stmtlst() #Stmtlst : {}
{
  (Stmt())*
}

void Stmt() : {}
{
  try{
    LOOKAHEAD(3)

    While() | If()

    |

    LOOKAHEAD(3)

    try{
      Assign()
    }
    catch(ParseException e){
       int[] kinds = {PVIRG, LPAR};
	   error_multskipto(e, kinds, "STATEMENT ASSIGN");
    }

    |

    (
	  try{
		  Call()
	  }
	  catch(ParseException e){
		  error_skipto_lookahead(e, PVIRG, "FUNCTION CALL");
	  }

	  try{
	     <PVIRG>
	  }
	  catch(ParseException e){
		  System.out.println("PVIIRG " + e.currentToken.image);
		  if(e.currentToken.image.compareTo("}") != 0)
		    error(e,"STATEMENT MISSING ;");
	  }
    )
  }
  catch(ParseException e){
    int[] kinds = {PVIRG, RCHAVETA};
	error_multskipto(e,kinds,"STATEMENT");
  }

}

void Assign() #Assign : {}
{
	Lhs() <ASSIGN>
    try{
      Rhs() <PVIRG>
	}
	catch(ParseException e){
	  error_skipto(e, PVIRG, "RIGHT ASSIGN");
    }
}

void Lhs() : {}
{
  Access()
}

void Rhs() #Rhs : {Token t1;}
{
  (Term() [( t1=<ARITH_OP> | t1=<BITWISE_OP> | t1=<ADDSUB_OP> ) {jjtThis.setValues(t1.image);}
   Term()])  | "[" ArraySize() "]"
}

void ArraySize() #ArraySize : {Token t1;}
{
  Access() | t1=<INTEGER>{jjtThis.setValues(t1.image);}
}

void Term() #Term : {Token t1=null, t2=null;}
{
  [t2=<ADDSUB_OP>]
  (t1=<INTEGER> | LOOKAHEAD(3) Call() | Access()) {jjtThis.setValues((t1 == null ? "": t1.image), (t2 == null ? "": t2.image));}
}

void Exprtest() #Exprtest : {Token t1;}
{
  try{
    <LPAR>
      Lhs() t1=<RELA_OP>{jjtThis.setValues(t1.image);} Rhs()
    <RPAR>
  }
  catch(ParseException e){
    error_skipto(e, RPAR, "EXPRTEST");
  }
}

void While() #While : {}
{
  <WHILE> Exprtest() <LCHAVETA> Stmtlst() <RCHAVETA>
}

void If() #If : {}
{
  <IF>

  try{
    Exprtest()
  }
  catch(ParseException e){
	error_skipto_lookahead(e, LCHAVETA, "IF");
  }

  <LCHAVETA> Stmtlst() <RCHAVETA>

  try{
	  [<ELSE> <LCHAVETA> Stmtlst() <RCHAVETA>]
  }
  catch(ParseException e){
	error_skipto(e, PVIRG, "ELSE");
  }
}

void Call() #Call: {Token t1, t2=null;}
{
  t1=<ID> ["." t2=<ID>] <LPAR>
  {jjtThis.setValues(t1.image, (t2 == null ? "": t2.image));}

  try{
    [ArgumentList()] <RPAR>
  }
  catch(ParseException e){
    error_skipto(e, RPAR, "FUNCTION CALL ARGS");
	if(getToken(1).image.compareTo("{") == 0){
	   System.out.println("Starting call RECOVERY");
	   CmpStmtLst();
	}
  }
}

void ArgumentList() #ArgumentList: {}
{
  Argument() (<VIRG> Argument())*
}

void Argument() #Argument : {Token t1;}
{
  (t1=<ID> | t1=<STRING> | t1=<INTEGER>)
  {jjtThis.setValues(t1.image);}
}

void Access() #Access : {Token t1, t2 = null;}
{
  t1=<ID> ("[" Index() "]" | ["." t2=<SIZE>])
  {jjtThis.setValues(t1.image, (t2 == null ? "": ".size"));}
}

void Index() #Index : {Token t1=null;}
{
  (t1=<INTEGER> | t1=<ID>) {jjtThis.setValues((t1 == null ? "" : t1.image));}
}
